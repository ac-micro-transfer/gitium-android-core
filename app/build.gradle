apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 3
        versionName "1.0.5"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

dependencies {
    implementation 'androidx.room:room-runtime:2.2.3'
    annotationProcessor 'androidx.room:room-compiler:2.2.3'
    implementation 'com.gitium:gitium-java:1.0.8'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'
}

group 'com.gitium'
version '1.0.5'

publishing {
    publications {
        maven(MavenPublication) {
            artifact androidSourcesJar
            artifactId 'gitium-android-core'
            artifact 'build/outputs/aar/app-release.aar'
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
    repositories {
        maven {
            def repoDir = 'D:\\Projects\\Github\\mvn'
            def releasesRepoUrl = "$repoDir/repository/releases"
            def snapshotsRepoUrl = "$repoDir/repository/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}
